plugins {
    id "java"
    id "maven-publish"
    id "me.qoomon.git-versioning" version "6.3.6"
}

repositories {
    mavenCentral()
}

group = "tech.pegasys"

version = "UNKNOWN"

gitVersioning.apply {
    refs {
        tag(".+") {
            version = "\${ref}"
        }
        branch("master") {
            version = "develop"
        }
        branch(".+") {
            version = "\${ref}"
        }
    }
    rev {
        version = "UNKNOWN"
    }
}

java {
    sourceCompatibility = 11
    targetCompatibility = 11
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set("javadoc")
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    repositories {
        maven {
            name = "cloudsmith"
            url = "https://api-g.cloudsmith.io/maven/consensys/maven/"
            credentials {
                username = System.getenv("CLOUDSMITH_USER")
                password = System.getenv("CLOUDSMITH_API_KEY")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            version project.version
            from components.java
            artifact sourcesJar

            versionMapping {
                usage("java-api") { fromResolutionOf("runtimeClasspath") }
                usage("java-runtime") { fromResolutionResult() }
            }
            pom {
                name = "${project.name}"
                url = "http://github.com/ConsenSys/jc-kzg-4844"
                description = "Java wrapper around C-KZG-4844"
                licenses {
                    license {
                        name = "The Apache License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }
                scm {
                    connection = "scm:git:git://github.com/ConsenSys/jc-kzg-4844.git"
                    developerConnection = "scm:git:ssh://github.com/ConsenSys/jc-kzg-4844.git"
                    url = "https://github.com/ConsenSys/jc-kzg-4844"
                }
            }
        }
    }
}

task checkJarContents() {
    doLast {
        exec {
            executable project.file("src/test/bash/checkResources.sh")
            args "${project.jar.outputs.files.getFiles()[0]}"
        }
    }
}